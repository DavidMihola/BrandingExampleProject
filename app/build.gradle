apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    // use this to change the APK base name from "app" to "brandingexample"
    project.archivesBaseName = "brandingexample"

    defaultConfig {
        applicationId "com.github.davidmihola.brandingexampleproject"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        good {
            storeFile file("./src/good/good.store")
            storePassword "goodpass"
            keyAlias "goodalias"
            keyPassword "goodaliaspass"
        }
        evil {
            storeFile file("./src/evil/evil.store")
            storePassword "evilpass"
            keyAlias "evilalias"
            keyPassword "evilaliaspass"
        }
    }

    buildTypes {
        // this part is default gradle config - you may want to enable proguard later
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        // the next part is not related to branding or productFlavors
        // but it will give your APK files more sensible names:

        // BTW: "all" is a method on Gradle's DomainObjectCollection
        // in contrast to "each" it is also applied to items that are
        // added to the collection _later_
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                String fileName = output.outputFile.name
                        .replace("-release", "-release-v" + defaultConfig.versionName + "-vc" + defaultConfig.versionCode)
                        .replace("-debug", "-debug-v" + defaultConfig.versionName + "-vc" + defaultConfig.versionCode)

                output.outputFile = new File(output.outputFile.parent, fileName)
            }
        }
    }

    productFlavors {
        // for each flavor the gradle uses the "main" source set AND
        // the source set found in a folder of the same name as the flavor itself

        // SO: all flavors use the "main" source set

        // additionally, this flavor uses the "whitelabel" source set (at src/whitelabel/*)
        whitelabel {
            applicationId "com.github.davidmihola.brandingexampleproject.whitelabel"
        }

        // additionally, this flavor uses the "good" source set (at src/good/*)
        good {
            applicationId "com.github.davidmihola.brandingexampleproject.good"
            signingConfig signingConfigs.good
        }

        // additionally, this flavor uses the "evil" source set (at src/evil/*)
        evil {
            applicationId "com.github.davidmihola.brandingexampleproject.evi"
            signingConfig signingConfigs.evil
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.0'

    // Parse dependencies
    // ATTENTION: Parse also need Parse*.jar in app/libs/
    compile 'com.parse.bolts:bolts-android:1.+'
    compile fileTree(dir: 'libs', include: 'Parse-*.jar')
}
